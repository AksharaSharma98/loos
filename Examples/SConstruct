#!/usr/bin/env python
#
# (c) 2008-2009 Tod D. Romo
#
# Grossfield Lab
# Department of Biochemistry & Biophysics
# University of Rochester Medical School
#
#


import sys
import os
import re
import shutil

# This can be reset in custom.py or here...

default_lib_path = '/usr/lib64'

### Tell SCons where to find LOOS
### This assumes you have installed LOOS.  If you have not,
### you will need to change the LOOSINC and LOOSLIB variables below and
### change the default path...
default_path_to_loos = '/opt/loos'

# Principal options...
clos = Variables('custom.py')
clos.Add('debug', 'Set to 1 to add -DDEBUG to build', 0)
clos.Add('release', 'Set to 1 to build with optimization on', 0)
clos.Add('profile', 'Set to 1 to build with profiling on', 0)


clos.Add(PathVariable('LOOS', 'Path to LOOS', default_path_to_loos, PathVariable.PathAccept))
clos.Add(PathVariable('LAPACK', 'Path to LAPACK', default_lib_path, PathVariable.PathAccept))
clos.Add(PathVariable('ATLAS', 'Path to ATLAS', default_lib_path + '/atlas', PathVariable.PathAccept))
clos.Add(PathVariable('ATLASINC', 'Path to ATLAS includes', '/usr/include/atlas', PathVariable.PathAccept))
clos.Add(PathVariable('BOOSTLIB', 'Path to BOOST libraries', default_lib_path, PathVariable.PathAccept))
clos.Add(PathVariable('BOOSTINC', 'Path to BOOST includes', '/usr/include', PathVariable.PathAccept))
clos.Add('BOOSTREGEX', 'Boost regex library name', 'boost_regex', PathVariable.PathAccept)
clos.Add('BOOSTPO', 'Boost program options library name', 'boost_program_options', PathVariable.PathAccept)
clos.Add('CXX', 'C++ Compiler', 'g++')
clos.Add(PathVariable('LIBXTRA', 'Path to additional libraries', '', PathVariable.PathAccept))
clos.Add(PathVariable('ALTPATH', 'Additional path to commands', '', PathVariable.PathAccept))
clos.Add(PathVariable('LIBS_OVERRIDE', 'Override linked libs', '', PathVariable.PathAccept))
clos.Add(PathVariable('LIBS_PATHS_OVERRIDE', 'Override paths to libs', '', PathVariable.PathAccept))
clos.Add(PathVariable('LOOSINC', 'Directory for LOOS include files (relative to LOOS)', 'include', PathVariable.PathAccept))
clos.Add(PathVariable('LOOSLIB', 'Directory for LOOS library files (relative to LOOS)', 'lib', PathVariable.PathAccept))

env = Environment(options = clos)
Help(clos.GenerateHelpText(env))

platform = sys.platform
env['platform'] = platform

LAPACK = env['LAPACK']
ATLAS = env['ATLAS']
ATLASINC = env['ATLASINC']
BOOSTLIB = env['BOOSTLIB']
BOOSTINC = env['BOOSTINC']
BOOSTREGEX = env['BOOSTREGEX']
BOOSTPO = env['BOOSTPO']
LOOS = env['LOOS']
LIBXTRA = env['LIBXTRA']
ALTPATH = env['ALTPATH']
LIBS_OVERRIDE = env['LIBS_OVERRIDE']
LIBS_PATHS_OVERRIDE = env['LIBS_PATHS_OVERRIDE']
LOOSINC = env['LOOSINC']
LOOSLIB = env['LOOSLIB']


# Fix include/lib locations...
LOOSINC = LOOS + '/' + LOOSINC
LOOSLIB = LOOS + '/' + LOOSLIB


if ALTPATH != '':
   buildenv = env['ENV']
   path = buildenv['PATH']
   path = ALTPATH + ':' + path
   buildenv['PATH'] = path


### Autoconf
if not env.GetOption('clean'):
   conf = Configure(env)
   if not conf.CheckType('ulong','#include <sys/types.h>\n'):
      conf.env.Append(CCFLAGS = '-DREQUIRES_ULONG')
   if not conf.CheckType('uint','#include <sys/types.h>\n'):
      conf.env.Append(CCFLAGS = '-DREQUIRES_UINT')
   env = conf.Finish()



### Compile-flags

debug_opts='-g -Wall -Wextra -fno-inline'
release_opts='-O3 -DNDEBUG -Wall'
profile_opts='-pg'

# Setup the general environment...
env.Append(CPPPATH = ['#', LOOSINC])

# Ideally, what's below should be added to the CPPPATH above, but
# doing so causes SCons to scan headers from that directory generating
# implicit dependencies.  SCons seems to mangle these so changing one
# file ends up forcing a complete rebuild.  Setting the include dirs
# directly solves this problem, but it does mean that changes to the
# include files in BOOST and ATLAS will not be picked up by SCons...
if BOOSTINC != '':
   env.Append(CPPFLAGS = ['-I' + BOOSTINC])
env.Append(LIBPATH = ['#', LOOSLIB, BOOSTLIB, LIBXTRA])
env.Append(LIBS = ['loos', BOOSTREGEX, BOOSTPO])
env.Append(LEXFLAGS=['-s'])

# Platform specific build options...
if platform == 'darwin':
   env.Append(LINKFLAGS = ' -framework vecLib')
elif platform == 'linux2':
   noatlas = 0

   # Determine linux variant
   fv = open('/proc/version', 'r')
   f = fv.read()
   

   LIBS_LINKED_TO = 'atlas lapack'
   LIBS_PATHS_TO = ATLAS + ' ' + LAPACK

   ### Note for OpenSUSE and Ubuntu...
   ### Older versions of those distros may require the gfortran
   ### package be linked in.  If you see strange link errors for
   ### unresolved symbols, try adding "gfortran" to the LIBS list
   ### for your OS below...

   # OpenSUSE doesn't have an atlas package, so use native lapack/blas
   if (re.search("[Ss][Uu][Ss][Ee]", f)):
      LIBS_LINKED_TO = 'lapack blas'
      LIBS_PATHS_TO = "LAPACK"

      # Ubuntu MAY require gfortran...more recent builds seem not to
   #elif (re.search("[Uu]buntu", f)):
      # LIBS_LINKED_TO = 'lapack blas gfortran'

      # Fedora & Similar
   #else


   if LIBS_OVERRIDE != '':
      LIBS_LINKED_TO = LIBS_OVERRIDE

   if LIBS_PATHS_OVERRIDE != '':
      LIBS_PATHS_TO = LIBS_PATHS_OVERRIDE

   env.Append(LIBS = Split(LIBS_LINKED_TO))
   env.Append(LIBPATH = Split(LIBS_PATHS_TO))


# CYGWIN does not have an atlas package, so use lapack/blas instead
elif (platform == 'cygwin'):
   env.Append(LIBS = ['lapack', 'blas'])
   env.Append(LIBPATH = [LAPACK])



# Determine what kind of build...
# No option implies debugging, but only an explicit debug defines
# the DEBUG symbol...  Yes, it's a bit obtuse, but it allows
# you to control the level of debugging output through the
# DEBUG definition...

release = int(env['release'])
debug = int(env['debug'])
profile = int(env['profile'])

# If debug is requested, make sure there is no optimization...
if (debug > 0):
   release=0

if int(release):
    env.Append(CCFLAGS=release_opts)
else:
   env.Append(CCFLAGS=debug_opts)

if (debug > 0):
   env.Append(CCFLAGS=" -DDEBUG=$debug")

# Profiling is independent of release/debug status...
if int(profile):
   env.Append(CCFLAGS=profile_opts)
   env.Append(LINKFLAGS=profile_opts)


# Allow overrides from environment...
if os.environ.has_key('CXX'):
   CXX = os.environ['CXX']
   print "Changing default compiler to ", CXX
   env['CXX'] = CXX

if os.environ.has_key('CCFLAGS'):
   CCFLAGS = os.environ['CCFLAGS']
   print "Changing CCFLAGS to ", CCFLAGS
   env['CCFLAGS'] = CCFLAGS


# Export for subsidiary SConscripts

Export('env')



# Invoke subsidiary sconscripts...

SConscript('SConscript')
