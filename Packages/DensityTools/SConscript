#!/usr/bin/env python
#  This file is part of LOOS.
#
#  LOOS (Lightweight Object-Oriented Structure library)
#  Copyright (c) 2011 Tod D. Romo
#  Department of Biochemistry and Biophysics
#  School of Medicine & Dentistry, University of Rochester
#
#  This package (LOOS) is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation under version 3 of the License.
#
#  This package is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.



import sys

Import('env')

clone = env.Clone()
clone.Prepend(LIBS = ['loos'])

### Library Generation
library_sources = 'GridUtils.cpp internal-water-filter.cpp water-hist-lib.cpp water-lib.cpp OptionsFramework.cpp'
library_headers = 'DensityGrid.hpp GridUtils.hpp internal-water-filter.hpp water-hist-lib.hpp water-lib.hpp OptionsFramework.hpp DensityOptions.hpp'

density_lib = clone.Library('loos_density', Split(library_sources))
clone.Prepend(LIBS=['loos_density'])
clone.Prepend(LIBPATH=['#/Packages/DensityTools'])
clone.Prepend(CPPPATH='#/Packages/DensityTools')


apps = 'gridinfo grid2ascii grid2xplor gridgauss gridscale gridslice gridmask blobid contained gridstat peakify pick_blob blob_stats water-inside water-extract'

list = []

for name in Split(apps):
    fname = name + '.cpp'
    prog = clone.Program(fname)
    list.append(prog)


### Installation specific

PREFIX = env['PREFIX']


density_tools = env.Install(PREFIX + '/bin', Split(apps))
density_libs = env.Install(PREFIX + '/lib', [density_lib])
density_headers = env.Install(PREFIX + '/include', Split(library_headers))
env.Alias('density_tools_package', density_tools + density_libs + density_headers)

Return('list')
